#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
                                               
#USER_SIMPLSHARP_LIBRARY "SimplSshClient"

// module inputs
digital_input initialize, connect, disconnect, debug, _skip_;
string_input tx$[250], _skip_;
string_input hostname[50];
analog_input port;
string_input username[20], password[20], debug_name[10];

// module outputs      
digital_output initialized_fb, connected_fb;
string_output _skip_,_skip_,_skip_, rx$, _skip_;

// global variables
SimplSshClient client;

function RegisterDelegates()
{
	RegisterDelegate(client, InitializedDataMethod, InitializedDataHandler);
	RegisterDelegate(client, ConnectionStateMethod, ConnectionStateHandler);
 	RegisterDelegate(client, ReceivedDataMethod, ReceivedDataHandler);
}

callback function InitializedDataHandler (integer state)
{
	initialized_fb = state;
}

callback function ConnectionStateHandler (integer state)
{
	connected_fb = state;
}

callback function ReceivedDataHandler(string data)
{
	rx$ = data;	 	
}
push initialize
{
	client.Initialize();
}                                   
push connect
{
	client.Connect();
}

push disconnect
{
 	client.Disconnect();
}

push debug
{
	client.TraceEnabled = 1;
}
release debug
{
	client.TraceEnabled = 0;
}
change hostname
{
	client.Hostname = hostname;
}
change port
{
	client.Port = port;
}
change username
{
	client.Username = username;
}
change password
{
	client.Password = password;
} 
threadsafe Change tx$
{
	client.Send(tx$);
}
function Main()
{
 	WaitForInitializationComplete();
	RegisterDelegates();
}                    
