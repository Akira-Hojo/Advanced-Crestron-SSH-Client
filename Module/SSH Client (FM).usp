#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
                                               
#USER_SIMPLSHARP_LIBRARY "SSHClient"

// module inputs
Digital_Input SshConnect, SshDisconnect, Debugging, _skip_;
String_Input Tx[250], _skip_, IPAddress[50], Username[50], Password[50];

// module outputs      
Digital_Output Connected_Fb, Debugging_Fb;
String_Output _skip_,_skip_,_skip_, Rx, _skip_;

// global variables
Integer connected;
SSHClientDevice client;

Function RegisterDelegates()
{
 	RegisterDelegate(client,SshRxDataToSimpl,DataRx);
	RegisterDelegate(client,SshStateChangeToSimpl,StatusRx);
}

Callback Function DataRx(string data)
{
	Rx = data;	 	
}

Callback Function StatusRx(integer status)
{
	connected = status;
	Connected_Fb = status;
}
                                   
Push SshConnect
{
	client.Connect(IPAddress, 22, Username, Password);
}

Push SshDisconnect
{
 	client.Disconnect();
}

Push Debugging
{
	if (client.Debug > 0)
	{
    	client.Debug = 0; 	
	}
	else client.Debug = 1;

	Debugging_Fb = client.Debug;
}

Threadsafe Change Tx
{
	// only send commands when connected
	if (connected) client.SendCommand(Tx);
}

Function Main()
{
 	WaitForInitializationComplete();
	RegisterDelegates();
}                    
